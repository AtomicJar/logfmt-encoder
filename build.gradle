plugins {
	id 'java-library'
	id 'maven-publish'
}

repositories {
	mavenCentral()
}

group = 'com.atomicjar'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

dependencies {
	implementation 'org.apache.commons:commons-text:1.9'

	api 'ch.qos.logback:logback-classic:1.2.10'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
	testImplementation 'org.assertj:assertj-core:3.24.2'
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/AtomicJar/logfmt-encoder")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			from(components.java)
		}
	}
}

if (hasProperty('buildScan')) {
	buildScan {
		termsOfServiceUrl = 'https://gradle.com/terms-of-service'
		termsOfServiceAgree = 'yes'
	}
}
